buildscript {
	ext {
		springBootVersion = '3.1.4'
	}

	repositories {
		mavenCentral()
		gradlePluginPortal()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:1.1.6"  // 최신 버전 사용 권장
	}
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'

	group = 'com.example'
	version = '1.0-SNAPSHOT'

	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
}

subprojects {
	sourceCompatibility = JavaVersion.VERSION_17  // Java 21 버전 호환 확인 필요

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
		testCompileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		// Lombok을 모든 모듈에서 사용 가능하도록 설정
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testCompileOnly 'org.projectlombok:lombok'          // Lombok을 테스트에서 사용
		testAnnotationProcessor 'org.projectlombok:lombok'  // Lombok을 테스트에서 사용할 때 어노테이션 프로세서 적용

		// https://mvnrepository.com/artifact/mysql/mysql-connector-java
		implementation 'mysql:mysql-connector-java:8.0.33'

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}
	test {
		useJUnitPlatform()
	}
}

/* QueryDSL 라이브러리를 사용하는 모듈 리스트 */
def querydslProjects = [
		project(":core"),
		project(":api"),
		project(":admin"),
		project(":batch")
]

configure(querydslProjects) {

	apply plugin: "io.spring.dependency-management"

	dependencies {
		compileOnly "com.querydsl:querydsl-jpa"
		compileOnly "com.querydsl:querydsl-core"
		implementation 'com.querydsl:querydsl-apt'

		annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
		annotationProcessor "jakarta.persistence:jakarta.persistence-api:2.2.3"
		annotationProcessor "jakarta.annotation:jakarta.annotation-api:1.3.5"
		annotationProcessor "jakarta.validation:jakarta.validation-api:3.1.0"
	}

	def generated = 'src/main/generated'

	sourceSets {
		main.java.srcDirs += [generated]
	}

	tasks.withType(JavaCompile).configureEach {
		options.generatedSourceOutputDirectory  = file(generated)
	}

	clean.doLast {
		file(generated).deleteDir()
	}
}
